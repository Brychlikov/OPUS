?start: _NL+ stmt+ 

?stmt: bid_stmt | branch

branch: test ":" body
body: bid_stmt | _NL _INDENT bid_stmt* branch* _DEDENT 


?test: expr cmp_op expr        -> cmp_test
    | test logic_op test       -> logic_test
    | expr
    | "(" test ")"
    | expr "in" expr           -> in_test
    | "else"                   -> else

?expr: product 
    | expr "+" product      -> add
    | expr "-" product      -> sub

?atom: _number              -> num
    | "(" expr ")"
    | suit                  -> suit_cards
    | suit "points"         -> suit_points
    | "$" NAME              -> var
    | "-" atom              -> neg
    | "not" test            -> logic_neg
    | card_list             -> card_list

?product: atom
    | product "*" atom      -> mul
    | product "/" atom      -> div


!cmp_op: (">" | ">=" | "==" | "!=" | "<=" | "<")

!logic_op: "or" | "and"

bid_stmt: "bid" _bid_num suit _NL+
    | "pass" _NL+

?card_list: /[AKQJT98765432]+/

!suit: ("♣" | "C")
    | ("♦" | "D")
    | ("♥" | "H")
    | ("♠" | "S")
    | "NT"
    | "@"

!_bid_num: _BID_NUM
!_number: NUMBER

COMMENT: /#[^\n]*/

%import common.CNAME -> NAME
%import common.NUMBER
%import common.WS_INLINE
%declare _INDENT _DEDENT
%ignore WS_INLINE

_BID_NUM: /[1-7]/
_NL: ( /\r?\n[\t ]*/ | COMMENT )+
